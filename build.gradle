plugins {
    id "java-library"
    id "eclipse"
    id "maven-publish"
    id "com.palantir.launch-config" version "1.2.0"
}

repositories {
    mavenCentral()
}

dependencies {
	implementation project(":minecraft-auth-api")
	implementation "com.google.code.gson:gson:2.8.6"
	implementation "net.sf.jopt-simple:jopt-simple:5.0.4"
	implementation "org.apache.logging.log4j:log4j-core:2.14.1"
	implementation "org.apache.logging.log4j:log4j-iostreams:2.14.1"
}

task runDownloader(type: JavaExec) {
	main "net.hycrafthd.minecraft_downloader.Main"
	args "--version", "1.16.5", "--output", "download", "--launch", "--run", "game", "--username", "xyz", "--password", "xyz"
	classpath sourceSets.main.runtimeClasspath
	def run = new File("$rootDir/run")
	if(!run.exists() ) {
		run.mkdirs()
	}
	workingDir run
}

group = "net.hycrafthd"
archivesBaseName = "minecraft_downloader"
version = "1.0.0-SNAPSHOT"

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
	withSourcesJar()
	withJavadocJar()
}

tasks.named("jar") {
	archiveClassifier = ""
	
	from "LICENSE"
	
	jar.dependsOn ":minecraft-auth-api:build"
	from "minecraft-auth-api/build/classes/java/main"
		
	manifest {
		attributes(
			"Implementation-Version": project.version,
			"Implementation-Vendor": "HyCraftHD, Luc_ZZ",
	        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        )
	}
}

tasks.withType(PublishToMavenRepository) {
	onlyIf {
		getValue("mavenpass") != null
	}
}

publishing {
	publish.dependsOn build

	repositories {
		maven {
			url "https://repo.u-team.info"
			credentials {
				username = "maven"
				password = getValue("mavenpass")
			}
		}
	}
	publications {
		mavenJava(MavenPublication) {
			from components.java
			
			groupId = project.group
			artifactId = project.archivesBaseName
			version = project.version
			
			pom {
				name = project.archivesBaseName
				description = "Minecraft-Downloader"
				url = "https://github.com/HyCraftHD/Minecraft-Downloader"
				scm {
					url = "https://github.com/HyCraftHD/Minecraft-Downloader"
					connection = "scm:git:git://github.com/HyCraftHD/Minecraft-Downloader.git"
					developerConnection = "scm:git:git@github.com:HyCraftHD/Minecraft-Downloader.git"
				}
				
				issueManagement {
					system = "github"
					url = "https://github.com/HyCraftHD/Minecraft-Downloader/issues"
				}
				licenses {
					license {
						name = "ARR"
						url = ""
						distribution = "repo"
					}
				}
				developers {
					developer {
						name = "HyCraftHD"
					}
					developer {
						name = "Luc_ZZ"
					}
				}
			}
			
			pom.withXml {
				Node pomNode = asNode()
				pomNode.dependencies."*".findAll() {
					it.artifactId.text() == "minecraft-auth-api"
				}.each() {
					it.parent().remove(it)
				}
			}
		}
	}
}

def getValue(name) {
	def envVariable = System.getenv(name)
	if(envVariable != null) {
		return envVariable
	} else {
		if (project.hasProperty(name)) {
			return project.getProperty(name)
		}
	}
	return null;
}