buildscript {
	ext.buildProps = file "build.properties"
	buildProps.withReader {
		def prop = new Properties()
		prop.load(it)
		ext.config = new ConfigSlurper().parse prop
	}
}

plugins {
	id "java-library"
	id "maven-publish"
	id "com.github.johnrengelman.shadow" version "8.1.0"
	id "org.gradlex.extra-java-module-info" version "1.3"
}

repositories {
	mavenCentral()
	maven {
		url "https://repo.u-team.info"
	}
}

extraJavaModuleInfo {
	automaticModule("net.sf.jopt-simple:jopt-simple", "jopt.simple")
}

dependencies {
	implementation group: "net.sf.jopt-simple", name: "jopt-simple", version: config.jopt_simple.version
	
	implementation group: "org.apache.logging.log4j", name: "log4j-api", version: config.log4j.version
	implementation group: "org.apache.logging.log4j", name: "log4j-core", version: config.log4j.version
	implementation group: "org.apache.logging.log4j", name: "log4j-iostreams", version: config.log4j.version
	
	implementation group: "com.google.code.gson", name: "gson", version: config.gson.version
	
	implementation group: "net.hycrafthd", name: "simple_minecraft_authenticator", version: config.simple_minecraft_authenticator.version
	
	implementation group: "org.tukaani", name: "xz", version: config.xz.version
}

group = "net.hycrafthd"
archivesBaseName = "minecraft_downloader"
version = "1.1.1"

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

sourceSets {
	main
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
	withSourcesJar()
	withJavadocJar()
}

tasks.named("jar") {
	from "LICENSE"
	
	manifest {
		attributes (
				"Implementation-Version": project.version,
				"Implementation-Vendor": "HyCraftHD, Luc_ZZ",
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
				"Fingerprint": project.findProperty("keystore.fingerprint") ?: "NONE",
				"Main-Class": "net.hycrafthd.minecraft_downloader.Main"
				)
	}
}

tasks.create("signJar") {
	dependsOn tasks.named("jar")
	mustRunAfter tasks.named("jar")
	
	enabled = project.hasProperty("createBuild")
	
	def keyStore = project.findProperty("keystore")
	def alias = project.findProperty("keystore.alias")
	def storePass = project.findProperty("keystore.password")
	def keyPass = project.findProperty("keystore.password_key")
	
	doLast {
		ant.signjar(
				keyStore: keyStore,
				alias: alias,
				storepass: storePass,
				keyPass: keyPass,
				jar: tasks.named("jar").get().archiveFile.get(),
				signedJar: tasks.named("jar").get().archiveFile.get()
				)
	}
}

shadowJar {
	archiveClassifier = "fat"
	
	transform(com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer)
	transform(com.github.jengelman.gradle.plugins.shadow.transformers.ApacheLicenseResourceTransformer)
	transform(com.github.jengelman.gradle.plugins.shadow.transformers.ApacheNoticeResourceTransformer)
	transform(com.github.jengelman.gradle.plugins.shadow.transformers.ServiceFileTransformer)
	
	manifest {
		attributes(
				"Multi-Release": "true"
				)
	}
}

tasks.create("signShadowJar") {
	dependsOn tasks.named("shadowJar")
	mustRunAfter tasks.named("shadowJar")
	
	enabled = project.hasProperty("createBuild")
	
	def keyStore = project.findProperty("keystore")
	def alias = project.findProperty("keystore.alias")
	def storePass = project.findProperty("keystore.password")
	def keyPass = project.findProperty("keystore.password_key")
	
	doLast {
		ant.signjar(
				keyStore: keyStore,
				alias: alias,
				storepass: storePass,
				keyPass: keyPass,
				jar: tasks.named("shadowJar").get().archiveFile.get(),
				signedJar: tasks.named("shadowJar").get().archiveFile.get()
				)
	}
}

tasks.named("assemble") {
	dependsOn tasks.named("signJar")
	dependsOn tasks.named("signShadowJar")
}
afterEvaluate {
	tasks.matching { task ->
		task.group == "publishing"
	}.each { task ->
		task.dependsOn("signJar")
		task.dependsOn("signShadowJar")
	}
}

tasks.withType(PublishToMavenRepository) {
	onlyIf {
		getValue("maven.password") != null
	}
}

publishing {
	repositories {
		maven {
			url "https://repo.u-team.info"
			credentials {
				username = "maven"
				password = getValue("maven.password")
			}
		}
	}
	publications {
		mavenJava(MavenPublication) {
			from components.java
			
			groupId = project.group
			artifactId = project.archivesBaseName
			version = project.version
			
			pom {
				name = project.archivesBaseName
				description = "Minecraft-Downloader"
				url = "https://github.com/HyCraftHD/Minecraft-Downloader"
				scm {
					url = "https://github.com/HyCraftHD/Minecraft-Downloader"
					connection = "scm:git:git://github.com/HyCraftHD/Minecraft-Downloader.git"
					developerConnection = "scm:git:git@github.com:HyCraftHD/Minecraft-Downloader.git"
				}
				
				issueManagement {
					system = "github"
					url = "https://github.com/HyCraftHD/Minecraft-Downloader/issues"
				}
				licenses {
					license {
						name = "Apache License 2.0"
						url = "https://github.com/HyCraftHD/Minecraft-Downloader/blob/main/LICENSE"
						distribution = "repo"
					}
				}
				developers {
					developer {
						name = "HyCraftHD"
					}
					developer {
						name = "Luc_ZZ"
					}
				}
			}
		}
	}
}

def getValue(name) {
	def envVariable = System.getenv(name)
	if(envVariable != null) {
		return envVariable
	} else {
		if (project.hasProperty(name)) {
			return project.getProperty(name)
		}
	}
	return null
}